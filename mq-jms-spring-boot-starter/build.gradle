/*
 * Copyright Â© 2018,2025 IBM Corp. All rights reserved.
 *
 * Licensed under the Apache License, Version 2.0 (the "License"); you may not use this file
 * except in compliance with the License. You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software distributed under the
 * License is distributed on an "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND,
 * either express or implied. See the License for the specific language governing permissions
 * and limitations under the License.
 */

buildscript {
  repositories {
    mavenCentral()
  }
  dependencies {
    // classpath 'io.spring.gradle:propdeps-plugin:0.0.9.RELEASE'
  }
}

plugins {
    id 'jacoco'
}

ext.mockitoVersion = '5.20.0'

dependencies {
  // Using "api" in this section means that the dependency ends up being listed
  // with scope=compile in the generated POM. So users of this package end up with
  // the transitive dependency Spring packages on their compile classpath too.

  // The MQ Java client is accessed from Maven Central Repository. The name of the
  // jar will vary for JMS2 and JMS3
  api group:mqGroup, name: mqJar, version: mqVersion

  // Spring
  api group: 'org.springframework',      name: 'spring-core',    version: springVersion
  api group: 'org.springframework',      name: 'spring-context', version: springVersion
  api group: 'org.springframework',      name: 'spring-beans',   version: springVersion
  api group: 'org.springframework',      name: 'spring-jms',     version: springVersion

  if (!springBootVersion.startsWith("4")) {
    api group: 'org.springframework.boot', name: 'spring-boot-starter', version: springBootVersion
    api group: 'org.springframework.boot', name: 'spring-boot', version: springBootVersion
    api group: 'org.springframework.boot', name: 'spring-boot-autoconfigure', version: springBootVersion
  } else {
    api group: 'org.springframework.boot', name: 'spring-boot-starter-jms', version: springBootVersion
    api group: 'org.springframework.boot', name: 'spring-boot-tx', version: springBootVersion
  }

  annotationProcessor group:'org.springframework.boot', name:'spring-boot-configuration-processor', version: springBootVersion

  api group: 'org.messaginghub',        name: 'pooled-jms',     version: pooledJmsVersion

  // Testing - these are not put into the pom.xml describing our uploaded jars
  testImplementation group: 'org.springframework.boot', name: 'spring-boot-starter-test', version: springBootVersion
  testImplementation group: 'org.mockito', name:'mockito-core', version: mockitoVersion
  
  // Setting up the dependencies needed for JUnit5 testing
  testImplementation group:'org.junit.jupiter', name:'junit-jupiter-api',    version: jUnitVersion
  testRuntimeOnly    group:'org.junit.jupiter', name:'junit-jupiter-engine', version: jUnitVersion
  testRuntimeOnly    group:'org.junit.platform', name:'junit-platform-launcher'
}

// This can be useful during development
tasks.withType(JavaCompile) {
    options.compilerArgs << '-Xlint:unchecked'
    options.deprecation = true
}

configurations {
    // We seem to get two versions of the org.json classes. Since we're not doing anything with
    // android, exclude it to avoid any warning messages.
    all*.exclude module: 'android-json'
}

tasks.test {
  useJUnitPlatform()
  jvmArgs "-javaagent:${configurations.testRuntimeClasspath.find { it.name.contains('mockito-core') }}", '-Xshare:off'
  testLogging {
       //events "PASSED", "SKIPPED", "FAILED", "STANDARD_OUT", "STANDARD_ERROR"
       events "PASSED", "SKIPPED", "FAILED"
      
  }
}
